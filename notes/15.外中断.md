# 王爽汇编第14章,外中断

[toc]

## 外中断概述

CPU在计算机系统中，除了能够执行指令，进行运算以外，还应该能够对外部设备进行控制，接收它们的输入，向它们进行输出（I/O能力）

PC系统的接口卡和主板上，装有各种接口芯片。这些外设接口芯片的内部有若干寄存器，CPU将这些寄存器当作`端口来访问`



CPU通过端口和外部设备进行联系

- 外设的输入不直接送入内存和CPU，而是送入相关的接口芯片的`端口`中；
- CPU向外设的输出也不是直接送入外设，而是先`送入端口`中，再由`相关的芯片送到外设`。
- CPU还可以向外设`输出控制命令`，而这些控制命令也是`先送到相关芯片的端口`中，然后再由相关的芯片根据命令`对外设实施控制`。



当CPU外部有需要处理的事情发生的时候，比如说，外设的输入到达，`相关芯片将向CPU发出相应的中断信息`。

CPU在执行完当前指令后，可以检测到发送过来的中断信息，引发中断过程，处理外设的输入。

## 可屏蔽中断

可屏蔽中断是CPU可以不响应的外中断。CPU是否响应可屏蔽中断，要看标志寄存器的IF位的设置。
当CPU检测到可屏蔽中断信息时，如果IF=1，则CPU在执行完当前指令后响应中断，引发中断过程；如果IF=0，则不响应可屏蔽中断。

可屏蔽中断信息来自于CPU外部，中断类型码是通过数据总线送入CPU的；而内中断的中断类型码是在CPU内部产生的。

中断过程中将IF置0的原因就是，在进入中断处理程序后，禁止其他的可屏蔽中断。
如果在中断处理程序中需要处理可屏蔽中断，可以用指令将IF置1。

8086CPU提供的设置IF的指令：`sti`，设置`IF=1`；`cli`，设置`IF=0`。

## 不可屏蔽中断

不可屏蔽中断是CPU必须响应的外中断。

当CPU检测到不可屏蔽中断信息时，则在执行完当前指令后，立即响应，引发中断过程。

对于8086CPU，`不可屏蔽中断的中断类型码固定为2`，所以中断过程中，不需要取中断类型码。

则不可屏蔽中断的中断过程为：

- 标志寄存器入栈，IF=0，TF=0；
- CS、IP入栈；
- IP =8，CS = 0AH

几乎所有由外设引发的外中断，都是可屏蔽中断。当外设有需要处理的事件（比如说键盘输入）发生时，相关芯片向CPU发出可屏蔽中断信息。

不可屏蔽中断是在系统中有必须处理的紧急情况发生时用来通知CPU的中断信息。


## 编写int 9中断例程
```assembly
;编程：在屏幕中间依次显示“a”~“z”，并可以让人看清。在显示的过程中，按下'Esc'键后，改变显示的颜色。

;完整功能代码：

assume cs:code

stack segment
	db 128 dup (0)
stack ends

data segment
	dw 0,0
data ends

code segment
start:	
	mov ax,stack
	mov ss,ax
	mov sp,128
	mov ax,data
	mov ds,ax
	mov ax,0
	mov es,ax

	push es:[9*4]
	pop ds:[0]
	push es:[9*4+2]
	pop ds:[2]		;将原来的int 9中断例程的入口地址保存在ds:0、ds:2单元中

	mov word ptr es:[9*4], offset int9
	mov es:[9*4+2], cs	;在中断向量表中设置新的int 9中断例程的入口地址

;显示字符串
	mov ax, 0b800h
	mov es, ax
	mov ah, 'a'
s:	
	mov  es:[160*12+40*2], ah
	call delay
	inc ah
	cmp ah, 'z'
	jna s
	mov ax,0
	mov es,ax

	push ds:[0]
	pop es:[9*4]
	push ds;[2]
	pop es;[9*4+2]   	;将中断向量表中int 9中断例程的入口恢复为原来的地址

	mov ax,4c00h
	int 21h

;将循环延时的程序段写为一个子程序
delay:	
	push ax 
	push dx
	mov dx, 2000h  ;用两个16位寄存器来存放32位的循环次数
	mov ax, 0
s1: 	
	sub ax, 1
	sbb dx, 0
	cmp ax, 0
	jne s1
	cmp dx, 0
	jne s1
	pop dx
	pop ax
	ret

;------以下为新的int 9中断例程--------------------

int9:	
	push ax
	push bx
	push es

	in al, 60h;从端口60h读出键盘的输入

	pushf ;标志寄存器入栈

	pushf   
	pop bx
	and bh,11111100b
	push bx
	popf	;TF=0,IF=0
	
	call dword ptr ds:[0] 	;对int指令进行模拟，调用原来的int 9中断例程

	cmp al,1
	jne int9ret

	mov ax,0b800h
	mov es,ax
	inc byte ptr es:[160*12+40*2+1]  ;属性增加1，改变颜色

int9ret:
	pop es
	pop bx
	pop ax
	iret

code ends

end start
```

CPU对外设输入的通常处理方法

- （1）外设的输入送入端口；
- （2）向CPU发出外中断（可屏蔽中断）信息；
- （3）CPU检测到可屏蔽中断信息，如果IF=1，CPU在执行完当前指令后响应中断，执行相应的中断例程；
- （4）可在中断例程中实现对外设输入的处理。

`端口`和`中断机制`，是CPU进行`I/O`的基础。

> 参考文章：
> https://blog.csdn.net/qq_39654127/article/details/88698911 王爽《汇编语言》笔记（详细）
